PROC IML;

   N=20;  * CANTIDAD DE INDIVIDUOS;
   M=4;   * OCASIONES DE MEDICIÓN;
   MEDIA=REPEAT(0,1,M);
   R={0.05 0 0 0, 0 0.2 0 0, 0 0 0.6 0, 0 0 0 0.05};
   MEDIA_EA=REPEAT(0,1,2); /* EF ALEATORIO EN INTERCEPTO Y EN PENDIENTE */
   D={0.1 0.05, 0.05 0.2};

      /* GENERACIÓN DE LOS ERRORES */
   CALL VNORMAL(E,MEDIA,R,N);
	  /* GENERACIÓN DE LOS EJECTOS ALEATORIOS */
   CALL VNORMAL(B,MEDIA_EA,D,N);

   Y=REPEAT(0,N,4); * PARA GUARDAR LOS VALORES DE LA RTA;
   T={0 3 6 12};    * TIEMPO;
   TTO=REPEAT(1,N/2,1)//REPEAT(2,N/2,1);  * VBLE QUE IDENTIFICA AL TRATAMIENTO. =1 PARA GRUPO1 Y 2 PARA EL GRUPO 2;

   * EFECTOS FIJOS;
   B01=-0.12;
   B02=0.08;
   B11=0.009;
   B12=0.008;

      /* GENERACIÓN DE LOS DATOS */
   DO A1=1 TO N;
      DO A2=1 TO 4;
         IF TTO[A1]=1 THEN 
         Y[A1,A2]=(B01+B[A1,1])+(B11+B[A1,2])*T[A2]+E[A1,A2];
         IF TTO[A1]=2 THEN
         Y[A1,A2]=(B02+B[A1,1])+(B12+B[A1,2])*T[A2]+E[A1,A2];
       END;
   END; 

   * REACOMODO LOS DATOS PARA CREAR EL DATA SET;
   DO U=1 TO N;
   RTA=RTA//T(Y[U,]);
   TPO=TPO//T(T);
   END; 

   TRAT=REPEAT(1,M*N/2,1)//REPEAT(2,M*N/2,1);
   IDENT=T(1:N);
   ID=IDENT@REPEAT(1,4,1);

   * CREACIÓN DEL DATA SET;
   DATOS=ID||TRAT||TPO||RTA;
   VARNAME={ID TTO T Y};
   CREATE DATOS FROM DATOS[COLNAME=VARNAME];
   APPEND FROM DATOS;
QUIT;
